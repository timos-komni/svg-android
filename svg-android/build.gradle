plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.timkom'
archivesBaseName = "svg-android"
version = '2.0.8'
def libraryVersionCode = 1
def libraryVersionName = version

description = """svg-android"""

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

android {
    namespace 'com.larvalabs.svgandroid'
    compileSdk 34

    defaultConfig {
        minSdk = 2
        versionCode = libraryVersionCode
        versionName = libraryVersionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

tasks.register('javadoc', Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = archivesBaseName
                version = libraryVersionName
                pom {
                    name = 'svg-android'
                    description = 'Brings SVG parsing and drawing functionality to Android devices. Forked from the original project at https://github.com/japgolly/svg-android (which was forked from http://code.google.com/p/svg-android/)'
                }
                afterEvaluate {
                    from components.findByName('release')
                }
            }
        }
    }
}